//Visualize password vs passwordless signins per week
SigninLogs
| project TimeGenerated, AuthenticationDetails
| where TimeGenerated > ago (180d)
| mv-expand todynamic(AuthenticationDetails)
| extend AuthMethod = tostring(parse_json(AuthenticationDetails).authenticationMethod)
| where AuthMethod != "Previously satisfied"
| summarize
    Password=countif(AuthMethod == "Password"),
    Passwordless=countif(AuthMethod in ("FIDO2 security key", "Passwordless phone sign-in", "Windows Hello for Business", "Mobile app notification", "X.509 Certificate"))
    by startofweek(TimeGenerated)
| render timechart
    with (
    xtitle="Week",
    ytitle="Signin Count",
    title="Password vs Passwordless signins per week")